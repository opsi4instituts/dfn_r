; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

DefVar $PreviousVersion$
DefVar $UninstallProgram$
DefVar $ActDir$
DefVar $UninstallPath$
DefVar $ActVersionDir$
DefVar $DesktopLinkName_32bit$
DefVar $DesktopLinkName_64bit$

DefStringList $directory_folders$
DefStringList $actual_version_string_list$
DefStringList $previous_version_string_list$

Set $directory_folders$ = getSubList(2 : -1, getOutStreamFromSection('dosbatch_dir'))
Set $ActVersionDir$     =  "R-" + strPart("%installingProdVersion%", "1", calculate(strPos("%installingProdVersion%","-") + "-" + "1" ))

Message "Uninstalling " + $PrettyName$ + " ..."

if (($KeepVersion$ = "remove all older") OR ($KeepVersion$	= "remove everything"))
  ; Iteration on directories
  for %actual_directory% in $directory_folders$ do sub_uninstall_older_prog
endif

if ($KeepVersion$	= "keep major versions")
	Set $PreviousVersion$ = ""
	for %actual_directory% in $directory_folders$ do sub_uninstall_minor_versions
endif
	
if ($KeepVersion$	= "remove everything")
  Set $ActDir$ = $ActVersionDir$ 
  sub_uninstall_prog
endif

if ($KeepVersion$	= "keep all older")
  comment "all versions of R will be kept, none will be uninstalled"
endif

; ----------------------------------------------------------------
; sub sections
; ----------------------------------------------------------------

[sub_uninstall_older_prog]
Set $ActDir$ = "%actual_directory%"
if not($ActDir$ = $ActVersionDir$)
  Set $UninstallProgram$ = $InstallDir$ + $ActDir$ + "\unins000.exe"
  Set $DesktopLinkName_32bit$ = "R i386 " + strPart ($ActDir$, "3", "8")  
  Set $DesktopLinkName_64bit$ = "R x64 " + strPart ($ActDir$, "3", "8") 
  if (not($InstallDir$ = '')) and FileExists($UninstallProgram$)
    Message "Uninstalling " + $ActDir$ + "..." 
    comment "Uninstall program found, starting uninstall " + $ActDir$
    Winbatch_uninstall_inno
    sub_check_exitcode
    LinkFolder_delete
  endif
endif

[sub_uninstall_prog]
Set $UninstallProgram$ = $InstallDir$ + $ActDir$ + "\unins000.exe"
Set $DesktopLinkName_32bit$ = "R i386 " + strPart ($ActDir$, "3", "8") 
Set $DesktopLinkName_64bit$ = "R x64 " + strPart ($ActDir$, "3", "8")  
if (not($InstallDir$ = '')) and FileExists($UninstallProgram$)
  Message "Uninstalling " + $ActDir$ + "..." 
  comment "Uninstall program found, starting uninstall " + $ActDir$
  Winbatch_uninstall_inno
  sub_check_exitcode
  LinkFolder_delete
endif

[sub_uninstall_minor_versions]
;split the version strings at the '.', because some might contain two instead of one digits
Set $actual_version_string_list$ 		= splitString("%actual_directory%", ".")
Set $previous_version_string_list$	= splitString($PreviousVersion$, ".")
if (count($previous_version_string_list$) > "0")
  if ( takeString(0,$previous_version_string_list$) = takeString(0,$actual_version_string_list$) )
    if ( takeString(1,$previous_version_string_list$) = takeString(1,$actual_version_string_list$) )
      Message "Uninstalling " + $PreviousVersion$ + "..." 
      Set $DesktopLinkName_32bit$ = "R i386 " + strPart ($PreviousVersion$ , "3", "8")  
      Set $DesktopLinkName_64bit$ = "R x64 " + strPart ($PreviousVersion$ , "3", "8") 
			Set $UninstallProgram$ = $InstallDir$ + $PreviousVersion$ + "\unins000.exe"
      if (not($InstallDir$ = '')) and FileExists($UninstallProgram$)
        comment "Uninstall program found, starting uninstall " + $PreviousVersion$ 
        Winbatch_uninstall_inno
				sub_check_exitcode
				LinkFolder_delete
      endif
    endif
  endif
endif
Set $PreviousVersion$ = "%actual_directory%"

[Winbatch_uninstall_inno]
"$UninstallProgram$" /SILENT /NORESTART

[dosbatch_dir]
dir "$InstallDir$" /b

[LinkFolder_delete]
; delete an shortcut to the installed exe on AllUsers desktop:
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $DesktopLinkName_32bit$
delete_element $DesktopLinkName_64bit$
; delete a folder from AllUsers startmenu:
;set_basefolder common_programs
;delete_subfolder $ProductId$
